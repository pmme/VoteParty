import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.5.21'
	id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

allprojects {
	apply plugin: 'org.jetbrains.kotlin.jvm'

	archivesBaseName = 'VoteParty'
	group 'me.clip'
	version '2.29'

	repositories {
		mavenCentral()

		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url = "https://repo.aikar.co/content/groups/aikar/" }
		maven { url = "https://papermc.io/repo/repository/maven-public/" }
		maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
		maven { url = "https://repo.glaremasters.me/repository/public/" }
	}

	dependencies {
		// kotlin
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

		// spigot
		compileOnly "org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT"

		// testing
		compileOnly "org.jetbrains.kotlin:kotlin-reflect"
		testImplementation "org.junit.jupiter:junit-jupiter-engine:5.6.0"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
	}

	test {
		useJUnitPlatform()
	}

	sourceSets {
		test.compileClasspath += configurations.compileOnly
		test.runtimeClasspath += configurations.compileOnly
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	compileJava {
		options.compilerArgs += ["-parameters"]
		options.fork = true
		options.forkOptions.executable = 'javac'
	}

	compileKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
		kotlinOptions.javaParameters = true
	}
	compileTestKotlin {
		kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
	}
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
	minimize()
	relocate("co.aikar.commands", "me.clip.voteparty.libs.acf")
	relocate("co.aikar.locales", "me.clip.voteparty.libs.locales")
	relocate("ch.jalu.configme", "me.clip.voteparty.libs.configme")
	relocate("org.inventivetalent", "me.clip.voteparty.libs.inventivetalent")
	relocate("net.kyori", "me.clip.voteparty.libs.kyori")
	relocate("com.cryptomorin.xseries", "me.clip.voteparty.libs.xseries")
	relocate("kotlin", "me.clip.voteparty.libs.kotlin")
	relocate("co.aikar.idb", "me.clip.voteparty.libs.idb")
	relocate("com.zaxxer", "me.clip.voteparty.libs.hikari")
	archiveFileName = "VoteParty-${project.version}.jar"
}


dependencies {

	// config
	implementation "ch.jalu:configme:1.2.0"

	// placeholderapi
	compileOnly "me.clip:placeholderapi:2.10.10"

	// NuVotifier hook
	compileOnly files("libs/nuvotifier-2.7.1.jar")

	// command handler
	implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"

	// Support for materials across all versions
	implementation "com.github.cryptomorin:XSeries:8.3.0"

	// json stuff
	implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
	implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"

	// MySQL?
	implementation "co.aikar:idb-core:1.0.0-SNAPSHOT"
	implementation "com.zaxxer:HikariCP:4.0.3"

	implementation project(":version")
	implementation project(":version_old")
	implementation project(":version_new")
}

processResources {
	filter ReplaceTokens, tokens: [version: version]
}
